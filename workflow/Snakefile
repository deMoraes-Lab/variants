# ReqSeq ID of reference genome
ID = "GCA_013166975.1"  # BL12 e. coli.

FORWARD = "tests/forward.fastq.gz"
REVERSE = "tests/reverse.fastq.gz"
NAME = "test_sample"

RESULTS_DIR = "results"
RESULTS_DIR_OTHER = f"{RESULTS_DIR}/other"
REFERENCE_DIR = f"{RESULTS_DIR}/reference"


ALL = f"{RESULTS_DIR_OTHER}/{NAME}.mpileup"

BOWTIE2_THREADS = 12


rule all:
    input:
        ALL,


rule download_reference:
    output:
        zip=f"{REFERENCE_DIR}/{ID}.zip",
    shell:
        """
        datasets download genome accession {ID} --filename {output} --include genome
        """


rule unzip_reference:
    input:
        zip=rules.download_reference.output.zip,
    output:
        fna=f"{REFERENCE_DIR}/{ID}.fna",
    shell:
        """
        unzip -o {input} -d {REFERENCE_DIR}

        # Flatten ncbi dir structure
        mv {REFERENCE_DIR}/ncbi_dataset/data/{ID}/* \
           {REFERENCE_DIR}

        # Rename fna
        mv {REFERENCE_DIR}/*.fna {output}

        # Remove crust
        rm -r {REFERENCE_DIR}/ncbi_dataset/ \
              {REFERENCE_DIR}/README.md
        """


rule index_reference:
    input:
        fna=rules.unzip_reference.output.fna,
    output:
        indexes=multiext(
            f"{REFERENCE_DIR}/{ID}",
            ".1.bt2",
            ".2.bt2",
            ".3.bt2",
            ".4.bt2",
            ".rev.1.bt2",
            ".rev.2.bt2",
        ),
    params:
        indexes_basename=f"{REFERENCE_DIR}/{ID}",
    threads: BOWTIE2_THREADS
    shell:
        """
        bowtie2-build --threads {threads} {input.fna} {params.indexes_basename}
        """


rule align:
    input:
        fq_forward=FORWARD,
        fq_reverse=REVERSE,
        indexes=rules.index_reference.output.indexes,
    output:
        sam=f"{RESULTS_DIR_OTHER}/{NAME}.sam",
    params:
        indexes_basename=f"{REFERENCE_DIR}/{ID}",
    threads: BOWTIE2_THREADS
    shell:
        """
        bowtie2 -p {threads} -x {params.indexes_basename} -1 {input.fq_forward} -2 {input.fq_reverse} -S {output.sam}
        """


rule get_bam:
    input:
        sam=rules.align.output.sam,
    output:
        bam=f"{RESULTS_DIR_OTHER}/{NAME}.bam",
    shell:
        """
        samtools view -S -b {input.sam} >| {output.bam}
        """


rule sort_bam:
    input:
        bam=rules.get_bam.output.bam,
    output:
        sbam=f"{RESULTS_DIR_OTHER}/{NAME}.sorted.bam",
    shell:
        """
        samtools sort {input.bam} -o {output.sbam}
        """


rule index_bam:
    input:
        sbam=rules.sort_bam.output.sbam,
    output:
        bai=f"{RESULTS_DIR_OTHER}/{NAME}.sorted.bam.bai",
    shell:
        """
        samtools index {input.sbam} -o {output.bai}
        """


rule get_mpileup:
    input:
        fna=rules.unzip_reference.output.fna,
        sbam=rules.sort_bam.output.sbam,
    output:
        mpileup=f"{RESULTS_DIR_OTHER}/{NAME}.mpileup",
    shell:
        """
        bcftools mpileup -f {input.fna} {input.sbam} > {output.mpileup}
        """


# # sort bam
# samtools sort "${OUT}.bam" -o "${OUT}.s.bam"
# # index bam
# samtools index "${OUT}.s.bam" -o "${OUT}.s.bai"
# # create mpileup file
# samtools mpileup -f "$REFERENCE" "${OUT}.s.bam" -o "${OUT}.mpileup"
# # create tsv file with variants
# varscan pileup2snp "${OUT}.mpileup" --min-var-freq 0.1  --p-value 0.01 >| "${OUT}.tsv"
