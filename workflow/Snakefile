configfile: "config/config.yaml"


# RefSeq ID of reference genome
ID = config["reference_genome_id"]

FORWARD = "tests/forward.fastq.gz"
REVERSE = "tests/reverse.fastq.gz"
NAME = "test_sample"

RESULTS_DIR = config["results"]["dir"]
RESULTS_DIR_ALIGN = config["results"]["alignments"]
REFERENCE_DIR = config["results"]["reference"]


ALL = f"{RESULTS_DIR_ALIGN}/{NAME}.mpileup"

BOWTIE2_THREADS = 12


rule all:
    input:
        ALL,


rule download_reference:
    output:
        zip=f"{REFERENCE_DIR}/{ID}.zip",
    shell:
        """
        datasets download genome accession {ID} --filename {output} --include genome
        """


rule unzip_reference:
    input:
        zip=rules.download_reference.output.zip,
    output:
        fna=f"{REFERENCE_DIR}/{ID}.fna",
    shell:
        """
        unzip -o {input} -d {REFERENCE_DIR}

        # Flatten ncbi dir structure
        mv {REFERENCE_DIR}/ncbi_dataset/data/{ID}/* \
           {REFERENCE_DIR}

        # Rename fna
        mv {REFERENCE_DIR}/*.fna {output}

        # Remove crust
        rm -r {REFERENCE_DIR}/ncbi_dataset/ \
              {REFERENCE_DIR}/README.md
        """


rule index_reference:
    input:
        fna=rules.unzip_reference.output.fna,
    output:
        indexes=multiext(
            f"{REFERENCE_DIR}/{ID}",
            ".1.bt2",
            ".2.bt2",
            ".3.bt2",
            ".4.bt2",
            ".rev.1.bt2",
            ".rev.2.bt2",
        ),
    params:
        indexes_basename=f"{REFERENCE_DIR}/{ID}",
    threads: BOWTIE2_THREADS
    shell:
        """
        bowtie2-build --threads {threads} {input.fna} {params.indexes_basename}
        """


rule align:
    input:
        fq_forward=FORWARD,
        fq_reverse=REVERSE,
        indexes=rules.index_reference.output.indexes,
    output:
        sam=f"{RESULTS_DIR_ALIGN}/{NAME}.sam",
    params:
        indexes_basename=f"{REFERENCE_DIR}/{ID}",
    threads: BOWTIE2_THREADS
    shell:
        """
        bowtie2 -p {threads} -x {params.indexes_basename} -1 {input.fq_forward} -2 {input.fq_reverse} -S {output.sam}
        """


rule get_bam:
    input:
        sam=rules.align.output.sam,
    output:
        bam=f"{RESULTS_DIR_ALIGN}/{NAME}.bam",
    shell:
        """
        samtools view -S -b {input.sam} >| {output.bam}
        """


rule sort_bam:
    input:
        bam=rules.get_bam.output.bam,
    output:
        sbam=f"{RESULTS_DIR_ALIGN}/{NAME}.sorted.bam",
    shell:
        """
        samtools sort {input.bam} -o {output.sbam}
        """


rule index_bam:
    input:
        sbam=rules.sort_bam.output.sbam,
    output:
        bai=f"{RESULTS_DIR_ALIGN}/{NAME}.sorted.bam.bai",
    shell:
        """
        samtools index {input.sbam} -o {output.bai}
        """


rule get_mpileup:
    input:
        fna=rules.unzip_reference.output.fna,
        sbam=rules.sort_bam.output.sbam,
    output:
        mpileup=f"{RESULTS_DIR_ALIGN}/{NAME}.mpileup",
    shell:
        """
        bcftools mpileup -f {input.fna} {input.sbam} >| {output.mpileup}
        """


rule call_variants:
    input:
        mpileup=rules.get_mpileup.output.mpileup,
    output:
        tsv=f"{RESULTS_DIR}/{NAME}.variants.tsv",
    shell:
        """
        varscan pileup2snp {input.mpileup} --min-var-freq 0.1 --p-value 0.01 >| {output.tsv}
        """
