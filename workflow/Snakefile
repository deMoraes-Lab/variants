from pathlib import Path


FORWARD = "forward.fastq.gz"
REVERSE = "reverse.fastq.gz"
REFERENCE = "GCA_013166975.1"  # BL12 e. coli.


RESULTS_DIR = "results"
RESULTS_DIR_OTHER = f"{RESULTS_DIR}/other"
REFERENCE_DIR = f"{RESULTS_DIR}/reference"


ALL = multiext(
    f"{REFERENCE_DIR}/{REFERENCE}",
    ".1.bt2",
    ".2.bt2",
    ".3.bt2",
    ".4.bt2",
    ".rev.1.bt2",
    ".rev.2.bt2",
)
BOWTIE2_BUILD_THREADS = 12


rule all:
    input:
        ALL,


rule download_reference:
    output:
        zip=f"{REFERENCE_DIR}/{REFERENCE}.zip",
    shell:
        """
        datasets download genome accession {REFERENCE} --filename {output} --include genome
        """


rule unzip_reference:
    input:
        zip=rules.download_reference.output.zip,
    output:
        fna=f"{REFERENCE_DIR}/{REFERENCE}.fna",
    shell:
        """
        unzip -o {input} -d {REFERENCE_DIR}

        # Flatten ncbi dir structure
        mv {REFERENCE_DIR}/ncbi_dataset/data/{REFERENCE}/* \
           {REFERENCE_DIR}

        # Rename fna
        mv {REFERENCE_DIR}/*.fna {output}

        # Remove crust
        rm -r {REFERENCE_DIR}/ncbi_dataset/ \
              {REFERENCE_DIR}/README.md
        """


rule index_reference:
    input:
        fna=rules.unzip_reference.output.fna,
    output:
        ALL,
    params:
        fna_basename=lambda w, input: Path(input.fna).name,
        alias={REFERENCE},
    threads: BOWTIE2_BUILD_THREADS
    shell:
        """
        cd {REFERENCE_DIR}
        bowtie2-build --threads {threads} {params.fna_basename} {params.alias}
        """
